# -------------------------------------------------
#  Universidad Simón Bolívar
#  Traductores e interpretadores - CI3725
#  Prof. Ricardo Monascal
#
#  Autores: Jorge Marcano   # Carnet 11-10566
#           Meggie Sánchez  # Carnet 11-10939
#
# Proyecto BOT - Etapa 2 - Gramática
# -------------------------------------------------

# Estructura de un programa en bot

PROGRAM -> DEC_LIST INST_EXE
		 | INST_EXE

INST_EXE -> TkExecute INST_CONT_LIST TkEnd

DEC -> TIPO TkBot ID_LIST COMP_LIST TkEnd

DEC_LIST -> TkCreate DEC
		  | DEC TkComa DEC_LIST

# Instrucciones de Controlador

INST_CONT -> TkActivate ID_LIST TkPunto
	       | TkAdvance ID_LIST TkPunto
	       | TkDeactivate ID_LIST TkPunto
	       | TkIf EXPR TkDosPuntos INST_CONT TkElse INST_CONT TkEnd
	       | TkIf EXPR TkDosPuntos INST_CONT TkEnd
		   | TkWhile EXPR TkDosPuntos INST_CONT TkEnd
		   | PROGRAM 							

# Instrucciones de Robot

INST_BOT -> TkStore EXPR TkPunto
		  | TkStore TkMe EXPR TkPunto				
		  | TkCollect TkPunto
		  | TkCollect TkMe TkPunto
		  | TkCollect TkAs ID TkPunto
		  | TkCollect TkMe TkAs ID TkPunto
		  | TkDrop EXPR TkPunto
		  | TkDrop TkMe EXPR TkPunto
		  | DIR TkPunto
		  | DIR TkMe TkPunto
		  | DIR EXPR TkPunto
		  | DIR TkMe EXPR TkPunto
		  | TkRead TkPunto
		  | TkRead TkMe TkPunto
		  | TkRead TkAs ID TkPunto
		  | TkRead TkMe TkAs ID TkPunto
		  | TkSend TkPunto
		  | TkSend TkMe TkPunto
		  | TkRecieve TkPunto
		  | TkRecieve EXPR TkPunto
		  | TkRecieve TkMe EXPR TkPunto

INST_CONT_LIST -> INST_CONT TkComa INST_CONT_LIST
		        | INST_CONT

INST_BOT_LIST -> INST_BOT TkComa INST_BOT_LIST
		       | INST_BOT

DIR -> TkLeft 
	 | TkRight
	 | TkUp
     | TkDown

ID -> TkIdent

ID_LIST -> ID TkComa ID_LIST
		 | ID

COMP -> TkOn EXPR TkDosPuntos INST_BOT TkEnd 
	  | TkOn STATE TkDosPuntos INST_BOT TkEnd

COMP_LIST -> COMP TkComa COMP_LIST
		   | COMP
		   | empty

empty -> 

STATE -> TkActivation
	   | TkDeactivation
	   | TkDefault

TIPO -> TkBool
	  | TkChar
	  | TkInt

EXPR -> TkNum
	  | TkIdent
	  | TkCaracter
	  | TkParAbre EXPR TkParCierra
	  | TkNegacion EXPR
	  | EXPR TkSuma EXPR
	  | EXPR TkResta EXPR
	  | EXPR TkMult EXPR
	  | EXPR TkDiv EXPR
	  | EXPR TkMod EXPR
	  | TkTrue
	  | TkFalse 
	  | EXPR TkConjuncion EXPR
	  | EXPR TkDisyuncion EXPR
	  | EXPR TkIgual EXPR
	  | EXPR TkMayor EXPR
	  | EXPR TkMayorIgual EXPR
	  | EXPR TkMenor EXPR
	  | EXPR TkMenorIgual EXPR
	  | EXPR TkDistinto EXPR

# PREGUNTAR POR SI TERMINA EN PUNTO DESPUES DE UNA SECUENCIA DE INSTRUCCIONES

# on EXPR, en condicion del comportamiento de robot, evalua nada más en bool, preguntar